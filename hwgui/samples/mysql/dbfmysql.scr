// dbf2mysql
Private cDbf, cTable, cQuery, i := 1, aStruct, vartmp
  if !Empty(cDbf:=SelectFile("xBase files( *.dbf )","*.dbf",Nil,"Select table to import")) .and. (cTable:=MsgGet("","Input mySQL table name:"))!=Nil
    use (cDbf)
    aStruct := dbStruct()
    // Building of CREATE TABLE query
    cQuery := "CREATE TABLE " + Lower(cTable) + " ("
    do while i <= Len(aStruct)
      cQuery += aStruct[i,1]
      if aStruct[i,2] == "C"
        cQuery += " char(" + LTrim(Str(aStruct[i,3])) + "),"
      elseif aStruct[i,2] == "M"
        cQuery += " text,"
      elseif aStruct[i,2] == "N"
        if aStruct[i,4] == 0
          cQuery += " int(" + LTrim(Str(aStruct[i,3])) + "),"
        else
          cQuery += " real(" + LTrim(Str(aStruct[i,3])) + "," + AllTrim(Str(aStruct[i,4])) + "),"
        endif
      elseif aStruct[i,2] == "D"
        cQuery += " date,"
      elseif aStruct[i,2] == "L"
        cQuery += " tinyint,"
      endif
      i++
    enddo
    cQuery := Left(cQuery, Len(cQuery) -1) + ")"

    // Execute CREATE TABLE query
    if sqlQuery( connHandle, cQuery) == 0
      go top
      while !Eof()
        // Building of INSERT query for each dbf record
        cQuery := "INSERT " + cTable + " SET "
        i := 1
        do while i <= Len(aStruct)
           cQuery += aStruct[i,1] + "="
           if aStruct[i,2] == "C" .OR. aStruct[i,2] == "M"
             cQuery += "'"+OemToAnsi(Fieldget(i))+"',"
           elseif aStruct[i,2] == "N"
             cQuery += Ltrim(Str(Fieldget(i))) + ","
           elseif aStruct[i,2] == "D"
             vartmp := Fieldget(i)
             if Empty(vartmp)
                cQuery += "''"
             else
                cQuery += "'" + Str(Year(vartmp),4) + "-" + PadL(Month(vartmp),2,"0") + "-" + PadL(Day(vartmp),2,"0") + "',"
             endif
           elseif aStruct[i,2] == "L"
             cQuery += Iif( FieldGet(i),"1","0" ) + ","
           endif
           i++
        enddo
        cQuery := Left(cQuery, Len(cQuery) - 1)
        if sqlQuery( connHandle, cQuery) != 0
           MsgStop( sqlGetErr( connHandle ) )
           exit
        endif
        skip
      enddo
    endif
    MsgInfo("Script executed")
  endif
